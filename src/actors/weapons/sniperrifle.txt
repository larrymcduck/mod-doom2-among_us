Actor M40Rifle : Weapon
{
  Obituary "%o was sniped out by %k's M40"
  Radius 20
  Height 16
  Inventory.PickupMessage "You got the M40A1 Sniper Rifle"
  Weapon.SlotNumber 6
  Weapon.KickBack 125
  Weapon.AmmoType "M40762Ammo"
  Weapon.AmmoUse 1
  Weapon.AmmoGive 12
  +Weapon.NoAlert
  +Weapon.NoAutoFire
  +INVENTORY.ALWAYSPICKUP
  Scale 0.8
  Decal BulletChip
  States
  {
  Spawn:
    SNPP A -1
    Stop
  Ready:
    SNPG A 0 A_JumpIfInventory("M40A1Zoom", 1, "ScopedReady")
    SNPG A 1 A_WeaponReady
    Loop
  ScopedReady:
    SNPS A 1 A_WeaponReady(1)
    Loop
  Deselect:
    SNPG A 0 A_JumpIfInventory("M40A1Zoom", 1, "ScopedDeselect")
    SNPG A 0 A_Lower
    SNPG A 1 A_Lower
    Loop
  ScopedDeselect:
    SNPG A 0 A_TakeInventory("M40A1Zoom", 2)
    SNPG A 0 A_PlaySound("M40Rifle/Scope")
    SNPG A 1 A_ZoomFactor(1.0)
    SNPG A 1 A_Lower
    Goto ScopedDeselect+3
  Select:
    SNPG A 0 A_Raise
    SNPG A 1 A_Raise
    Loop
  Fire:
    SNPG A 0 A_JumpIfInventory("M40A1Zoom", 1, "ScopedFire")
    SNPG A 0 A_AlertMonsters
    SNPG A 0 A_FireBullets (0.0, 0.0, -1, 115, "M40Puff")
    SNPG A 0 A_PlayWeaponSound("M40Rifle/Fire")
    SNPG A 0 A_Quake(2, 2, 0, 1)
    SNPG A 0 A_SetPitch(Pitch-0.5)
    SNPF AB 2
    SNPF CDE 3
    SNPG ABC 3
    SNPG DEF 2
    SNPG H 2 A_PlayWeaponSound("M40Rifle/Bolt1")
    SNPG IJ 2
    SNPG K 3
    SNPG JI 2
    SNPG H 2 A_PlayWeaponSound("M40Rifle/Bolt2")
    SNPG FED 3
    SNPG CB 4
    SNPG A 2
    SNPG A 1 A_ReFire 
    Goto Ready
  ScopedFire:
    SNPS A 0 A_AlertMonsters
    SNPS A 0 A_FireBullets (0.0, 0.0, -1, 115, "M40Puff")
    SNPS A 0 A_PlayWeaponSound("M40Rifle/Fire")
    SNPS A 0 A_Quake(2, 2, 0, 1)
    SNPS A 0 A_SetPitch(Pitch-0.5)
    SNPS A 28
    SNPS A 2 A_PlayWeaponSound("M40Rifle/Bolt1")
    SNPS A 11
    SNPS A 2 A_PlayWeaponSound("M40Rifle/Bolt2")
    SNPS A 19
    SNPS A 1 A_ReFire
    Goto Ready
  AltFire:
    SNPS A 0 A_JumpIfInventory("M40A1Zoom", 2, "ZoomOut")
    SNPS A 0 A_JumpIfInventory("M40A1Zoom", 1, "ZoomMore")
    SNPS A 0 A_PlaySound("M40Rifle/Scope")
    SNPS A 0 A_GiveInventory("M40A1Zoom", 1)
    SNPS A 1 A_ZoomFactor(2.0)
    Goto Ready
  ZoomMore:
    SNPS A 0 A_PlaySound("M40Rifle/Scope")
    SNPS A 0 A_GiveInventory("M40A1Zoom", 1)
    SNPS A 1 A_ZoomFactor(10.0)
    Goto Ready
  ZoomOut:
    SNPG A 0 A_TakeInventory("M40A1Zoom", 2)
    SNPG A 0 A_PlaySound("M40Rifle/Scope")
    SNPG A 1 A_ZoomFactor(1.0)
    Goto Ready
  ScopedReady:
    SNPS A 1 A_WeaponReady(1)
    Loop
  }
}

ACTOR M40A1Zoom : Inventory { Inventory.MaxAmount 2 }


Actor M40Puff : BulletPuff
{
  +NoExtremeDeath
}

Actor M40762Ammo : Ammo
{
  Inventory.PickupMessage "Picked up some bullets for the M40."
  +INVENTORY.ALWAYSPICKUP
  Inventory.Amount 4
  Inventory.MaxAmount 16
  Ammo.BackpackAmount 16
  Ammo.BackpackMaxAmount 32
  Inventory.Icon "SNPPB0"
  States
  {
    Spawn:
      SNPP B -1
      stop
  }
}

Actor M40762AmmoBox : M40762Ammo
{
  Inventory.PickupMessage "Picked up a box of bullets for the M40."
  Inventory.Amount 8
  States
  {
    Spawn:
      SNPP C -1
      Stop
  }
}
